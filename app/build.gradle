apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.fabric'

def getSecretProperty(name) {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('secret.properties').newDataInputStream())

  return properties.getProperty(name.toString())
}

def FABRIC_API_KEY = getSecretProperty('fabric.api.key')
def TWITTER_API_KEY = "\"${getSecretProperty('twitter.api.key')}\""
def TWITTER_API_SECRET = "\"${getSecretProperty('twitter.api.secret')}\""

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.2"

  signingConfigs {
    release {
      storeFile file("../yslibrary.jks")
      keyAlias "yslibrary"
      storePassword getSecretProperty("keystorePassword")
      keyPassword getSecretProperty("keystoreKeyPassword")
    }
  }

  defaultConfig {
    applicationId "net.yslibrary.monotweety"
    minSdkVersion 21
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"

    buildConfigField 'String', 'TWITTER_API_KEY', TWITTER_API_KEY
    buildConfigField 'String', 'TWITTER_API_SECRET', TWITTER_API_SECRET

    manifestPlaceholders = [fabricApiKey: FABRIC_API_KEY]

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
    debug {
      debuggable true
      applicationIdSuffix '.debug'
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

ext.support_lib_version = '24.2.1'
ext.rxbinding_version = '0.4.0'
ext.conductor_version = '2.0.2-SNAPSHOT'
dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  compile "com.android.support:appcompat-v7:$support_lib_version"
  compile "com.android.support:recyclerview-v7:$support_lib_version"
  compile "com.android.support:design:$support_lib_version"

  compile('com.twitter.sdk.android:twitter:2.0.1@aar') {
    transitive = true;
  }
  compile('com.crashlytics.sdk.android:crashlytics:2.6.4@aar') {
    transitive = true;
  }

  compile 'com.google.dagger:dagger:2.7'
  kapt 'com.google.dagger:dagger-compiler:2.7'
  provided 'org.glassfish:javax.annotation:10.0-b28'

  compile 'io.reactivex:rxjava:1.2.0'
  testCompile 'io.reactivex:rxjava:1.2.0'
  compile 'io.reactivex:rxandroid:1.2.1'
  testCompile 'io.reactivex:rxandroid:1.2.1'
  compile 'io.reactivex:rxkotlin:0.60.0'

  compile "com.jakewharton.rxbinding:rxbinding-kotlin:$rxbinding_version"
  compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$rxbinding_version"
  compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
  compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:$rxbinding_version"

  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

  compile "com.bluelinelabs:conductor:${conductor_version}"
  compile "com.bluelinelabs:conductor-support:${conductor_version}"
  compile "com.bluelinelabs:conductor-rxlifecycle:${conductor_version}"

  compile 'com.jakewharton.timber:timber:4.3.1'
  compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

  compile 'net.yslibrary.rxeventbus:rxeventbus:1.0.1'

  compile 'org.jetbrains.anko:anko-common:0.9'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })


  testCompile 'junit:junit:4.12'
}
repositories {
  mavenCentral()
  maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
  maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
}
